WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ "--" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

keywords = {
    "var"
  | "const"
  | "unit"
  | "never"
  | "end"
  | "function"
  | "include"
  | "not"
  | "as"
  | "and"
  | "or"
  | "do"
  | "then"
  | "if"
  | "elseif"
  | "else"
  | "while"
  | "begin"
  | "structure"
  | "variants"
  | "union"
  | "type"
  | "return"
  | "break"
  | "continue"
  | "effect"
  | "try"
  | "handle"
  | "resume"
  | primitive_type
}

identifier = @{ !keywords ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

integer = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

float = @{ ("+" | "-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }

char   = ${ "'" ~ char_c ~ "'" }
char_c =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("'" | "\\" | "0" | "e" | "r" | "t" | "n")
  | "\\" ~ ("x" ~ ASCII_HEX_DIGIT{2})
}

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char_s* }
char_s =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "0" | "e" | "r" | "t" | "n")
  | "\\" ~ ("x" ~ ASCII_HEX_DIGIT{2})
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{6})
}

unit = { "unit" }

literal = { integer | float | char | string | unit }

primitive_type = @{
    ("i" | "u") ~ ("8" | "16")
  | ("i" | "u" | "f") ~ ("32" | "64")
  | "usize"
  | "char"
  | "unit"
}

structure_type_entry = { identifier ~ ":" ~ mirisa_type_inner }

structure_type = { "structure" ~ (structure_type_entry ~ ("," ~ structure_type_entry)*)? ~ "end" }

variants_type = { "variants" ~ (identifier ~ ("," ~ identifier)*)? ~ "end" }

union_type = { "union" ~ (structure_type_entry ~ ("," ~ structure_type_entry)*)? ~ "end" }

operation_args = { (mirisa_type_inner ~ ("," ~ mirisa_type_inner)*)? }

operation = { identifier ~ "(" ~  operation_args ~ ")" ~ mirisa_type_inner }

effect = { "effect" ~ identifier ~ (operation ~ ("," ~ operation)*)? ~ "end" }

primary_type = { primitive_type | identifier | structure_type | variants_type | union_type }

pointer = { "*" ~ "mut"? }

array_t = { "[" ~ integer? ~ "]" }

type_modifier = _{ pointer+ | array_t+ }

mirisa_type_inner = { type_modifier? ~ primary_type }

is_mut = { "mut"? }

mirisa_type = { is_mut ~ mirisa_type_inner }

array = { "[" ~ (expression ~ ("," ~ expression)*)? ~ "]" }

structure_entry = { identifier ~ "=" ~ expression }

structure = { "structure" ~ (structure_entry ~ ("," ~ structure_entry)*)? ~ "end" }

union = { "union" ~ structure_entry ~ "end" }

primary_expression = _{ literal | identifier | array | structure | union | "(" ~ expression ~ ")" }

call = { "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }

indexing = { "[" ~ expression ~ "]" }

field_access = { "." ~ identifier }

dereference = { "." ~ "*" }

expression_postfix = { call | indexing | field_access | dereference }

expression_level_bottom = { primary_expression ~ expression_postfix* }

op_level_prefix = { "not" | "&" }

expression_level_prefix = { op_level_prefix* ~ expression_level_bottom }

expression_level_as = { expression_level_prefix ~ ("as" ~ mirisa_type)* }

op_level_mul = { "*" | "/" | "%" }

expression_level_mul = { expression_level_as ~ (op_level_mul ~ expression_level_as)* }

op_level_add = { "+" | "-" }

expression_level_add = { expression_level_mul ~ (op_level_add ~ expression_level_mul)* }

op_level_comp = { "=" | "/=" | ("<" | ">") ~ "="? }

expression_level_comp = { expression_level_add ~ (op_level_comp ~ expression_level_add)* }

expression_level_and = { expression_level_comp ~ ("and" ~ expression_level_comp)* }

expression_level_or = { expression_level_and ~ ("or" ~ expression_level_and)* }

expression = _{ expression_level_or }

include = { "include" ~ include_path }

declaration = { "let" ~ identifier ~ ":" ~ mirisa_type ~ ("=" ~ expression)? }

type_alias = { "alias" ~ identifier ~ "=" ~ mirisa_type }

op = _{ op_level_add | op_level_mul }

reassignment_op = @{ (op | ":") ~ "=" }

reassignment_postfix = _{ indexing | dereference | field_access }

lexpression_inner = { identifier ~ reassignment_postfix* }

lexpression = { lexpression_inner | "(" ~ lexpression ~ ")" }

reassignment = { lexpression ~ reassignment_op ~ expression }

body = { statement* }

if_then = { "then" ~ body }

if_elseif = { "elseif" ~ expression ~ "then" ~ body }

if_else = { "else" ~ body }

mirisa_if = {
    "if" ~ expression ~ if_then ~ if_elseif* ~ if_else? ~ "end"
}

mirisa_while = {
    "while" ~ expression ~ "do" ~ body ~ "end"
}

do_while = {
    "do" ~ body ~ "while" ~ expression ~ "end"
}

handler_arg = { is_mut ~ identifier }

handler_args = { (handler_arg ~ ("," ~ handler_arg)*)? }

operation_handler = {
    identifier ~ "(" ~ handler_args ~ ")" ~ body ~ "end"
}

handler = { "handle" ~ identifier ~ operation_handler* }

mirisa_try = { "try" ~ body ~ handler+ ~ "end" }

block = { "begin" ~ body ~ "end" }

mirisa_return = { "return" ~ expression }

mirisa_break = { "break" }

mirisa_continue = { "continue" }

statement = {
    declaration
  | type_alias
  | reassignment
  | mirisa_if
  | mirisa_while
  | do_while
  | mirisa_try
  | block
  | expression
  | mirisa_return
  | mirisa_break
  | mirisa_continue
}

function_arg = { identifier ~ ":" ~ mirisa_type }

function_args = { (function_arg ~ ("," ~ function_arg)*)? }

effect_set = { ("&" ~ "{" ~ (identifier ~ ("," ~ identifier)*)? ~ "}")? }

function = {
    "function" ~ identifier ~ "(" ~ function_args ~ ")" ~ mirisa_type_inner ~ effect_set ~ "do" ~ body ~ "end"
}

include_path = ${ (ASCII_ALPHANUMERIC | "\\ ")+ ~ ("/" ~ (ASCII_ALPHANUMERIC | "\\ ")+)* }

item = _{ include | declaration | type_alias | effect | function }

program = _{ SOI ~ item* ~ &EOI }
